module javastringtemplate.StringTemplate

import java.JavaPlatform

/* StringTemplate is a library written in Java by the ANTLR guy that
supports pretty printing. */
StringTemplate : Library;
    = "http://www.stringtemplate.org/";
    facilitates TemplateProcessing;
    facilitates PrettyPrinting;
    uses Java.
StringTemplateLanguage : TemplateLanguage;
    = "https://github.com/antlr/stringtemplate4/blob/master/doc/index.md#templates";
    ^implements StringTemplate.
    
/* The template language is embedded into Java by wrapping it into an object */
StringTemplateObject : ValueLanguage;
    = "https://github.com/antlr/stringtemplate4/blob/master/src/org/stringtemplate/v4/ST.java";
    ^implements StringTemplate;
    subsetOf JVMObject .
wrap : StringTemplateLanguage -> StringTemplateObject.
StringTemplate implements wrap.
?template : Artifact;
    elementOf StringTemplateLanguage;
    hasRole Template;
    manifestsAs File.
?stObject1 : Artifact;
    elementOf StringTemplateObject;
    hasRole Template;
    hasRole Wrapper;
    manifestsAs Transient.
wrap(?template) |-> ?stObject1.
   
/* The template can then be filled with concrete values by calling the toString method 
of an input object */
setVar : JVMString # JVMObject # StringTemplateObject -> StringTemplateObject.
StringTemplate implements setVar.
?variable : Artifact;
    elementOf JVMString;
    manifestsAs Transient;
    hasRole Identifier.
?value : Artifact;
    elementOf JVMObject;
    manifestsAs Transient;
    hasRole Value.
?stObject2 : Artifact;
    elementOf StringTemplateObject;
    hasRole Template;
    hasRole Wrapper;
    manifestsAs Transient.
setVar(?variable,?value,?stObject1) |-> ?stObject2 .

/* The render function can then be used to unwrap the produced String */
render : StringTemplateObject -> JVMString.
StringTemplate implements render.
?prettyPrintedString : Artifact;
    elementOf JVMString;
    manifestsAs Transient;
    hasRole Value.
render(?stObject2) |-> ?prettyPrintedString.


//TODO: Combined ANTLR and StringTemplate as noted at StringTemplate's doc
//TODO: StringTemplate can be used with other languages