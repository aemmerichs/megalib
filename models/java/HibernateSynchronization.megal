module Hibernate

// import JavaPlatform
// import JDBC
Java : ProgrammingLanguage
JavaPlatform : Platform // has to be imported

Persistence : ProgrammingDomain
ObjectRelationalMapping : ProgrammingDomain
    subdomainOf Persistence
SQLWare : TechnologySpace
AuxiliaryXMLware : TechnologySpace

DatabaseProgramming : ProgrammingDomain
JDBC : Library
    uses Java
    partOf JavaPlatform
    supports DatabaseProgramming
    
Hibernate : Framework
    uses Java
    partOf JavaPlatform
    supports ObjectRelationalMapping
    belongsTo SQLWare
    belongsTo AuxiliaryXMLware
    uses JDBC

SQL : DatabaseManipulationLanguage
SQLDML : TransformationLanguage
SQLDDL : SchemaLanguage
SQLDDL subsetOf SQL

Configuration : Role
?hibernate.cfg : ConnectionSettings<XML>
?hibernate.cfg hasRole Configuration
?hibernate.cfg manifestsAs File

DBImage : DataRepresentationLanguage
?db1 : Artifact<DBImage>
?db1 hasRole Database
?db2 : Artifact<DBImage>
?db2 hasRole Database

ObjectRelationalMapping : Concept
?modelpartconfiguration : Artifact<XML>
?modelpartconfiguration hasRole Configuration
?modelpartconfiguration uses ObjectRelationalMapping

?createStmt : Artifact<SQLDDL>
?createStmt hasRole Schema
POJO : Concept
?modelpartClass : Artifact<Java>
?modelpartClass hasRole Model
?modelpartClass manifestsAs File
?modelpartClass uses POJO

?table1 : Artifact<DBImage>
?table1 hasRole Database
?table1 partOf ?db1
Hibernate.EmbeddedSQL : EmbeddedQueryLanguage
?query : Artifact<Hibernate.EmbeddedSQL>
?query hasRole Query
?modelpartconfiguration defines loadObject
loadObject : DBImage # Hibernate.EmbeddedSQL -> ?modelobject1
loadObject(?db1,?query) |-> ?modelobject1

?modelobject1 : Artifact<JavaObject>
?modelobject1 hasRole Value
?modelobject1 manifestsAs Transient
?modelobject1 correspondsTo ?table1

?modifyingClass : Artifact<Java>
?modifyingClass hasRole Program
?modifyingClass manifestsAs File
?modifyingClass implements modifyObject
modifyObject : JavaObject -> JavaObject
modifyObject(modelobject1) |-> modelobject2
?modelobject2 : Artifact<JavaObject>
?modelobject2 hasRole Value
?modelobject2 manifestsAs Transient

?modelpartconfiguration defines commit
commit : JavaObject # DBImage -> DBImage
commit(?modelObject2,?db1) |-> ?db2
?table2 : Artifact<DBImage>
?table2 hasRole Database
?table2 partOf ?db2
?table2 correspondsTo ?modelobject2

?db1 conformsTo ?createStmt
?db2 conformsTo ?createStmt
?modelobject1 conformsTo ?modelpartClass
?modelobject2 conformsTo ?modelpartClass

?modelpartClass correspondsTo ?createStmt
?table1 correspondsTo ?modelobject1
?table2 correspondsTo ?modelobject2