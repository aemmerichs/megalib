module hadoop.HadoopHDFS

import hadoop.Hadoop
import hadoop.HadoopYARN
import java.JavaPlatform 
/*
HDFS definition is starting here
*/
?HDFSFileSystem : FileSystem;
	= "https://hadoop.apache.org/docs/r2.7.2/hadoop-project-dist/hadoop-common/filesystem/index.html";
	= "https://hadoop.apache.org/docs/r2.7.2/hadoop-project-dist/hadoop-hdfs/HdfsDesign.html#NameNode_and_DataNodes".
HDFSFile : FileFormat;
	= "https://hadoop.apache.org/docs/r2.7.2/hadoop-project-dist/hadoop-common/FileSystemShell.html#get".
LocalFile : FileFormat;
	= "https://en.wikipedia.org/wiki/Computer_file".
FilePath : ReferenceLanguage;
	= "https://en.wikipedia.org/wiki/Path_(computing)".

SequenceFile : FileFormat;
	= "https://wiki.apache.org/hadoop/SequenceFile";
	subsetOf HDFSFile.
PlainText : FileFormat;
	= "https://en.wikipedia.org/wiki/Plain_text"; 	
	subsetOf HDFSFile.

?someFile : Artifact;
	elementOf LocalFile;
	manifestsAs File;
	hasRole Database.
?someHdfFile : Artifact;
	elementOf HDFSFile;
	manifestsAs File;
	hasRole Database;
	partOf ?HDFSFileSystem.
?preHDFSState : Artifact;
	elementOf HDFSFile;
	manifestsAs File+;
	hasRole Database;	
	partOf ?HDFSFileSystem.
?postHDFSState : Artifact;
	elementOf HDFSFile;
	manifestsAs File+;
	hasRole Database;
	partOf ?HDFSFileSystem.	
?filePath : Artifact;
	elementOf FilePath;
	manifestsAs Transient;
	hasRole Reference.

/*
We can write an input file to the file system
*/		
write : LocalFile # FilePath # HDFSFile -> HDFSFile.
write(?someFile, ?filePath ,?preHDFSState) |-> ?postHDFSState.
read : FilePath # HDFSFile -> LocalFile.
read(?filePath ,?preHDFSState) |-> ?someFile.


/*
------------------------------------------------------------------------

HDFS is the primary distributed storage used by Hadoop application. It is a module of hadoop. May we want to call this one Framework 
instead of component
*/
HDFS : Component;
	="https://hadoop.apache.org/docs/r1.2.1/hdfs_design.html";
	facilitates DistributedFilesystem;
	partOf Hadoop;
	uses Java;
	reuses HadoopYARN;
	implements HDFSFile;
	implements FilePath;
	implements JSON;
	implements write;
	implements read.


WebHDFS : WebService;
	= "https://hadoop.apache.org/docs/stable/hadoop-project-dist/hadoop-hdfs/WebHDFS.html";
	partOf HDFS;
	facilitates REST;
	uses HTTP;
	realizes SoftwareInterface;
	uses Java.

LibHdfs : API;
	= "https://hadoop.apache.org/docs/stable/hadoop-project-dist/hadoop-hdfs/LibHdfs.html";
	uses C;
	realizes SoftwareInterface;
	partOf HDFS.

HDFSApi : API;
	= "http://hadoop.apache.org/docs/current/api/org/apache/hadoop/fs/package-summary.html";
	implements LocalFile;
	realizes SoftwareInterface;
	uses Java;
	partOf HDFS.

HDFSShellCommands : Program;
	= "https://hadoop.apache.org/docs/stable/hadoop-project-dist/hadoop-common/FileSystemShell.html";
	uses Java;
	uses ShellScript;
	realizes SoftwareInterface;
	partOf HDFS.