module java.NashornJavaScriptEngine

import java.JavaPlatform

/* The Java library Nashorn supports the use of
JavaScript in existing Java code.*/
Nashorn.JavaScript : ProgrammingLanguage
    = "http://www.w3schools.com/js/"
    supports WebApplicationDevelopment
    embeddedInto Java
Nashorn : Platform
    = "http://www.oracle.com/technetwork/articles/java/jf14-nashorn-2126515.html"
    implements Nashorn.JavaScript
    uses Java

/* In a use case an application may have some java class
that contains the embedded JavaScript job. */
?MyJavaApp : Application
    uses Java
?ScriptJob : Artifact
    partOf ?MyJavaApp
    elementOf Java
    hasRole Executable
    manifestsAs File
    uses Nashorn
?DemoScript : Artifact
    partOf ?ScriptJob
    elementOf Nashorn.JavaScript
    hasRole Executable
    manifestsAs Fragment

/* Nashorn can the be used to evaluate the JavaScript
fragment. It returns a java object wrapping the result
of the evaluation. */
?returnValue : Artifact
    elementOf JavaObject
    hasRole Output
    manifestsAs Transient
eval : Nashorn.JavaScript -> JavaObject
eval(?DemoScript) |-> ?returnValue
Nashorn implements eval
 