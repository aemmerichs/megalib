module antlr.CodeGeneration

import antlr.Core 
import antlr.App

/*
@Description:
    The lexer implementation is generated.
@Rationale:
    See parser call in demo
*/
?grammar : File+; 
	elementOf G4;
	hasRole Grammar;
    defines ?InputLanguage;
    partOf ?App.
?lexerFiles : File+;
	elementOf ANTLRJava;
    hasRole Lexer;
    partOf ?App.
generateLexer : G4 -> ANTLRJava .
ANTLR implements generateLexer.
generateLexer(?grammar) |-> ?lexerFiles .

/*
@Description:
    The parser implementation is generated.
@Rationale:
    See parser call in demo
*/
?parserFiles : File+;
	elementOf ANTLRJava;
    hasRole Parser;
    partOf ?App.
generateParser : G4 -> ANTLRJava .
ANTLR implements generateParser.
generateParser(?grammar) |-> ?parserFiles .