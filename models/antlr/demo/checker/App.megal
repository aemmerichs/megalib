module antlr.demo.checker.App

import antlr.App where { 
    [ MegaL / ?InputLanguage,
      Checker / ?App ] }
import antlr.demo.checker.ListenerForParser
import antlr.demo.checker.ListenerForParserError


/*
@Description:
The Checker helps the user to find syntactical errors in a megamodel written in MegaL.

@Rationale:
See https://github.com/softlang/megalib/
*/
MEGALIB :: "https://github.com/softlang/megalib".
MegaL : KnowledgeRepresentationLanguage;
    = "MEGALIB::wiki".
Checker = "MEGALIB::blob/master/checker/src/org/java/megalib/checker";
    implements MegaL.

/*
@Description:
the checker takes the model as input and returns a list of problems.

@Rationale:
See https://github.com/softlang/megalib/wiki "Use Checker"
*/
checkModel : MegaL -> JVMObjectLanguage .
Checker implements checkModel. 

/*
@Description:
    lexing and parsing with listener and error-listener

@Rationale:
    See imports:

	lex(?someMegaModel) |-> ?tokenstream.

	?parserObject conformsTo ?ANTLRJavaParserFiles.	
	addListener(?parserObject, ?ParserErrorListener) |-> ?parserObjectWithErrorListener.
	addListener(?parserObject, ?aNTLRParserListener) |-> ?parserObjectWithANTLRListener.

	parse(?tokens, ?parserObjectWithErrorListener) |-> (?concreteSyntaxTree, ?parserObjectWithListenersAfterApplication).

	getErrorListeners(?parserObjectWithListenersAfterApplication) |-> ?returnedParserErrorListeners.
	getListeners(?parserObjectWithListenersAfterApplication) |-> ?returnedANTLRParserListeners.
*/
getErrors: ErrorListenerObjectsLanguage -> JVMString.
checker implements getErrors.
?errors : Artifact;
	manifestsAs Transient;
	elementOf JVMString.
getErrors(?returnedParserErrorListeners) |-> ?errors.

/*
@Description:
    returning megaModel Object

@Rationale:
    The megaModel, which was created during parsing,    
    with the help of the listener, is returned here.
*/
MegaModelObjectLanguage : ValueLanguage;
    = "MEGALIB::tree/master/checker/src/org/java/megalib/models";
	subsetOf JVMObject.
?megaModel : Artifact;
	manifestsAs Transient;
	elementOf MegaModelObjectLanguage.
getMegaModel : ANTLRListenerObjectsLanguage -> MegaModelObjectLanguage.
checker implements getMegaModel.
getMegaModel (?returnedANTLRParserListeners) |-> (?megaModel).

/*
@Description:
    finding and returning warnings
@Rationale:
    See https://github.com/softlang/megalib/wiki
*/
getWarnings: MegaModelObjectLanguage -> JVMList.
checker implements getWarnings.
?warnings : Artifact;
	manifestsAs Transient;
	elementOf JVMList.
getWarnings(?megaModel) |-> ?warnings.

/*
@Description:
    Concrete lexer and parsers are generated.
@Rationale:
    TODO: We need linking to a local project
*/
checkerLexer = "https://github.com/softlang/megalib/tree/master/checker".
checkerParser = "https://github.com/softlang/megalib/tree/master/checker".