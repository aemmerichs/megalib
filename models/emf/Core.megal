module emf.Core

import eclipse.Core 

/* Given namespaces */
emfJavaDoc :: "http://download.eclipse.org/modeling/emf/emf/javadoc/2.8.0/org/eclipse/emf/".


/* The eclipse modeling framework belongs to the model driven engineering
technologies. It's a platform that consists of eclipse plugins. */
Emf : Platform;
    = "https://eclipse.org/modeling/emf/";
    = "emfJavaDoc::common/EmfPlugin.html";
    = "wikipedia::Eclipse_Modeling_Framework";
    uses Java;
    partOf Eclipse;
    supports ModelDrivenEngineering.

/* EMF builds a bridge between three technological spaces, namely Java, UML and XML related
technologies. */     
Emf belongsTo JavaWare;
    bridgesTo XMLWare;
    bridgesTo UMLWare.

/* Its main component is EmfCore which contains Ecore's 
metametamodel written in Java. The root class 
provides an interface EObject to any Ecore objects
corresponding to instances of a metamodel. */    
Emf.ECore : Plugin;
    = "emfJavaDoc::";
    partOf Emf;
    uses Java.
ecorePluginNS :: "platform::org.eclipse.emf.ecore.source_*.jar".
ecorePluginJar : File;
    ~= "ecorePluginNS::";
    elementOf Jar;
    hasRole Plugin;
    ^deployedAs Emf.ECore.
EObjectLanguage : ValueLanguage; //instances of EObject etc.
    = "ecorePluginNS::org/eclipse/emf/ecore/EObject";
    = "emfJavaDoc::ecore/EObject.html";
    subsetOf JVMObjects.
ecoreMetaModel : File+;
    ~= "ecorePluginNS::org/eclipse/emf/ecore";
    elementOf Java;
    hasRole MdeMetamodel;
    partOf Emf.ECore;
    conformsTo ecoreMetaModel;
    defines EObjectLanguage.