module emf.EMF

import java.JavaPlatform
import Eclipse
import XML

/* The eclipse modeling framework belongs to the model driven engineering
technologies. It's an eclipse plugin implemented in Java. */
EMF : Plugin
    = "https://eclipse.org/modeling/emf/"
    uses Java
    partOf Eclipse

/* Its main component is EMFCore which implements the object model for
EMF models ECore. */
ECore : SchemaLanguage
    = "http://download.eclipse.org/modeling/emf/emf/javadoc/2.7.0/org/eclipse/emf/ecore/package-summary.html"
    subsetOf Java
EMFCore : Component
    = "http://download.eclipse.org/modeling/emf/emf/javadoc/2.7.0/overview-tree.html"
    partOf EMF
    uses Java

/* The EMF persistence framework allows you to persist ecore models in a subset of XMI */
?PDF : FileFormat
    = "https://en.wikipedia.org/wiki/Portable_Document_Format"
XMI : MarkupLanguage
    = "https://en.wikipedia.org/wiki/XML_Metadata_Interchange"
    subsetOf XML
XMISpec : Artifact
    = "http://www.omg.org/spec/XMI/2.5.1/PDF"
    elementOf ?PDF
    hasRole Specification
    manifestsAs File 
    defines XMI
ECore.XMI : DataRepresentationLanguage
    = "http://download.eclipse.org/modeling/emf/emf/javadoc/2.5.0/org/eclipse/emf/ecore/xmi/XMIResource.html"
    subsetOf XMI
EMFPersistence : Component
    = "http://download.eclipse.org/modeling/emf/emf/javadoc/2.7.0/index.html?org/eclipse/emf/ecore/package-summary.html"
    = "http://www.vogella.com/tutorials/EclipseEMFPersistence/article.html"
    uses Java
    partOf EMFCore
    uses Serialization
    implements ECore.XMI


/* An Ecore model is a metamodel for the DSL which is a language subset of ECore */
?MyModels.ECore : SchemaLanguage
?MyModels.XMI : DataRepresentationLanguage
    subsetOf ECore.XMI
?MyMeta.ecore : Artifact
    elementOf ECore
    hasRole MdeMetamodel
    manifestsAs File
    defines ?MyModels.XMI
    defines ?MyModels.ECore

/* A Java object can be serialized in the XMI subset for a DSL */
?MyModels.JavaObject : ObjectGraph
    subsetOf JavaObject
?MyModel : Artifact 
    elementOf ?MyModels.JavaObject
    hasRole MdeModel
    manifestsAs Transient
    conformsTo ?MyMeta.ecore
?MyModel.xmi : Artifact
    elementOf ?MyModels.XMI
    hasRole MdeModel
    manifestsAs File
    conformsTo ?MyMeta.ecore
    correspondsTo ?MyModel
saveModel : ?MyModels.JavaObject -> ?MyModels.XMI
EMFPersistence implements saveModel
saveModel(?MyModel) |-> ?MyModel.xmi
loadModel : ?MyModels.XMI -> ?MyModels.JavaObject
EMFPersistence implements loadModel
loadModel(?MyModel.xmi) |-> ?MyModel