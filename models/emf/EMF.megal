module emf.EMF

import java.JavaPlatform
import Eclipse
import XML

/* The eclipse modeling framework belongs to the model driven engineering
technologies. It's an eclipse plugin implemented in Java. */
EMF : Plugin
    = "https://eclipse.org/modeling/emf/"
    uses Java
    partOf Eclipse
    supports ModelDrivenEngineering

/* Its main component is EMFCore which implements the meta model for
EMF models in Java based on the ECore specification. */
EMFCore : Component
    = "http://download.eclipse.org/modeling/emf/emf/javadoc/2.7.0/overview-tree.html"
    partOf EMF
    uses Java
EcoreJava : ProgrammingLanguage
    = "http://download.eclipse.org/modeling/emf/emf/javadoc/2.7.0/org/eclipse/emf/ecore/package-summary.html"
    subsetOf Java
EMF implements EcoreJava
?EcoreMetaModel : Artifact
    elementOf EcoreJava
    hasRole Schema
    manifestsAs File+
    defines EcoreJava

/* XMI is an OMG standard for persisting models. */
?PDF : FileFormat
    = "https://en.wikipedia.org/wiki/Portable_Document_Format"
XMI : MarkupLanguage
    = "https://en.wikipedia.org/wiki/XML_Metadata_Interchange"
    subsetOf XML
XMISpec : Artifact
    = "http://www.omg.org/spec/XMI/2.5.1/PDF"
    elementOf ?PDF
    hasRole Specification
    manifestsAs File 
    defines XMI

/* An important component is the Generator that takes a generation
configuration which is itself a model persisted in XMI. */
CodeGenerator : Component
    = "http://www.eclipse.org/modeling/emf/docs/2.x/tutorials/clibmod/clibmod_emf2.0.html"
    partOf EMF
    uses Java

GeneratorModel : ConfigurationLanguage
    = "http://download.eclipse.org/modeling/emf/emf/javadoc/2.11/org/eclipse/emf/codegen/ecore/genmodel/GenModel.html"
    subsetOf XMI

?genModel : Artifact
    elementOf GeneratorModel
    hasRole Configuration
    manifestsAs File

?javaModel : Artifact
    elementOf EcoreJava
    hasRole MdeModel
    manifestsAs File+

generateJava : GeneratorModel -> EcoreJava
CodeGenerator implements generateJava
generateJava(?genModel) |-> ?javaModel

/* The EMF persistence framework allows you to persist ecore models in a subset of XMI */
ECore.XMI : DataRepresentationLanguage
    = "http://download.eclipse.org/modeling/emf/emf/javadoc/2.5.0/org/eclipse/emf/ecore/xmi/XMIResource.html"
    subsetOf XMI
EMFPersistence : Component
    = "http://download.eclipse.org/modeling/emf/emf/javadoc/2.7.0/index.html?org/eclipse/emf/ecore/package-summary.html"
    = "http://www.vogella.com/tutorials/EclipseEMFPersistence/article.html"
    uses Java
    partOf EMFCore
    uses Serialization
    implements ECore.XMI


/* An Ecore model is a metamodel for the DSL which is a language subset of ECore */
?MyModels.ECore : SchemaLanguage
?MyModels.XMI : DataRepresentationLanguage
    subsetOf ECore.XMI
?MyMeta.ecore : Artifact
    elementOf EcoreJava
    hasRole MdeMetamodel
    manifestsAs File
    defines ?MyModels.XMI
    defines ?MyModels.ECore

/* A Java object can be serialized in the XMI subset for a DSL */
?MyModels.JavaObject : ObjectGraph
    subsetOf JavaObject
?MyModel : Artifact 
    elementOf ?MyModels.JavaObject
    hasRole MdeModel
    manifestsAs Transient
    conformsTo ?MyMeta.ecore
?MyModel.xmi : Artifact
    elementOf ?MyModels.XMI
    hasRole MdeModel
    manifestsAs File
    conformsTo ?MyMeta.ecore
    correspondsTo ?MyModel
saveModel : ?MyModels.JavaObject -> ?MyModels.XMI
EMFPersistence implements saveModel
saveModel(?MyModel) |-> ?MyModel.xmi
loadModel : ?MyModels.XMI -> ?MyModels.JavaObject
EMFPersistence implements loadModel
loadModel(?MyModel.xmi) |-> ?MyModel